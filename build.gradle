apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'jacoco'

buildscript {
    ext.kotlin_version = '1.4.10'
    apply from: "${rootDir}/gradle/dependencies.gradle"

  repositories {
    google()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:4.1.3'
    classpath pluginDependencies.gradle
    classpath pluginDependencies.coveralls
    classpath pluginDependencies.errorprone
    classpath pluginDependencies.dependencyUpdates
      classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

task testReport(type: TestReport, group: 'Build') {
  description = 'Generates an aggregate test report'
  destinationDir = file("${buildDir}/reports/allTests")
}

allprojects {
  repositories {
    google()
    maven { url 'https://plugins.gradle.org/m2' }
  }

  group = GROUP
  version = VERSION_NAME
}

subprojects {
  apply plugin: 'idea'
  apply plugin: 'net.ltgt.errorprone'
  apply from: "${rootDir}/gradle/dependencies.gradle"

  pluginManager.withPlugin('com.android.library') {
    apply plugin: 'maven-publish'

    task sourceJar(type: Jar) {
      classifier 'sources'
      from android.sourceSets.main.java.srcDirs
    }

    publishing {
      publications {
        bar(MavenPublication) {
          groupId = project.group
          artifactId = project.name
          version = project.version
          artifact("$buildDir/outputs/aar/${artifactId}-release.aar")
          artifact tasks.sourceJar
        }
      }
    }
  }

  dependencies {
    errorprone dependenciesList.errorprone
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}